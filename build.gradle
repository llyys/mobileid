apply plugin: 'java'
apply plugin: 'maven'

group = 'com.codeborne'
archivesBaseName = 'mobileid'
version = '1.0.0'
sourceCompatibility = 1.7
targetCompatibility = 1.7

defaultTasks 'clean', 'copy-libs', 'copy-test-libs', 'test', 'install'

repositories{
  mavenCentral()
}

sourceSets {
  main {
    java {
      srcDir 'src'
    }
  }
  test {
    java {
      srcDir 'test'
    }
  }
}

compileJava.options.debugOptions.debugLevel = "source,lines,vars"
[compileJava, compileTestJava, javadoc]*.options.collect {options ->
  options.encoding = 'UTF-8'
}

[compileJava, compileTestJava]*.options.collect {options ->
  options.debug = true
  options.deprecation = true
  options.compilerArgs.add '-Xlint'
  options.compilerArgs.add '-Xlint:-unchecked'
  options.compilerArgs.add '-Xlint:-serial'
  options.compilerArgs.add '-Xlint:-rawtypes'
}

dependencies {
  compile 'org.apache.axis:axis:1.4'
  compile 'org.apache.axis:axis-jaxrpc:1.4'
  compile 'org.apache.commons:commons-io:1.3.2'
  compile 'org.apache.commons:commons-lang3:3.1'
  compile 'org.apache.commons:commons-compress:1.3'
  compile 'commons-codec:commons-codec:1.8'
  compile 'commons-discovery:commons-discovery:0.4'
  compile 'wsdl4j:wsdl4j:1.6.2'
  compile 'log4j:log4j:1.2.17'
  compile 'org.jdom:jdom2:2.0.5'
  compile 'javax.servlet:servlet-api:2.5'
  compile files('lib/jdigidoc-3.10.0-777.jar')

  testCompile 'junit:junit:4.10'
  testCompile 'org.hamcrest:hamcrest-all:1.1'
  testCompile 'org.mockito:mockito-all:1.9.0'
}

task 'copy-libs'(type: Sync) {
  from configurations.compile
  into "$buildDir/libs"
}

task 'copy-test-libs'(type: Sync) {
  from configurations.testCompile
  into "$buildDir/libs-test"
}

task sourcesJar(type: Jar, dependsOn:classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.0-milestone-8a'
  jarFile = './gradle-wrapper/gradle-wrapper.jar'
  scriptFile = './gradle'
}

//fixes Intellij IDEA dependencies not being updated when gradle deps updated.
allprojects {
    apply plugin: 'idea'
}
